buildscript {
    repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
      classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'

repositories {
  jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

tasks.eclipse.dependsOn(cleanEclipse)

eclipse {
	classpath {
		// make sure all subproject source sets are added to the parent .classpath
		sourceSets = allprojects.collect { it.sourceSets.collect { it } }.flatten()
		// make sure all subproject dependencies are added to the parent .classpath
		allprojects.configurations.compile.collect { it }.flatten().each { plusConfigurations << it }
		//closure executed after .classpath content is loaded from existing file
		//and after gradle build information is merged
		file.whenMerged { cp ->
			cp.entries.removeAll { cpentry ->
				cpentry.kind == "src" && allprojects.find { "/" + it.name == cpentry.path }
			}
		}
	}
}

//Declare the plugin Id and Version
group = 'org.wedoit4u.gradle.plugins.OracleCommercePlugin'
version = '0.0.1'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:////Workspace//myRepo")
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'http://www.wedoit4u.org/'
  vcsUrl = 'https://bitbucket.org/snagarajan/oracle-commerce-gradle.git'
  description = 'Oracle Commerce Gradle Plugin'
  tags = ['oracle commerce', 'atg', 'endeca']

  plugins {
    oracleCommercePlugin {
      id = 'org.wedoit4u.gradle.plugins.OracleCommercePlugin'
      displayName = 'Oracle Commerce plugin'
    }
  }
}
